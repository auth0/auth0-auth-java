plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.diffplug.spotless" version "6.25.0"
}

ext {
    POM_ARTIFACT_ID = 'auth0-springboot-api'
    POM_NAME = 'auth0-springboot-api'
    POM_DESCRIPTION = 'Auth0 Springboot Authentication Library with DPoP Support'
    POM_PACKAGING = 'jar'
}

apply from: rootProject.file('gradle/versioning.gradle')

group = GROUP
version = getVersionFromFile()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation project(':auth0-api-java')
    
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'


    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}

spotless {
    java {
        googleJavaFormat()
        target 'src/**/*.java'
    }
}


logger.lifecycle("Using version ${version} for ${name} group ${group}")
apply from: rootProject.file('gradle/maven-publish.gradle')

